#pragma kernel CSMain
#pragma kernel Clear

StructuredBuffer<float4> _InstancePosScale;
RWStructuredBuffer<float> _InstancePress01;

Texture2D<float4> _InteractionRT;
SamplerState sampler_InteractionRT;

uint _InstanceCount;

float4 _InteractionCamPosXZ;
float4 _InteractionCamParams;

[numthreads(64, 1, 1)]
void CSMain(uint3 id : SV_DispatchThreadID)
{
    uint index = id.x;
    if (index >= _InstanceCount)
        return;

    float3 worldPos = _InstancePosScale[index].xyz;

    float2 camXZ = _InteractionCamPosXZ.xy;
    float invWorldSize = _InteractionCamParams.z;
    
    float2 uv = (worldPos.xz - camXZ) * invWorldSize + 0.5;

    if (any(uv < 0.0) || any(uv > 1.0))
    {
        _InstancePress01[index] = 0.0;
        return;
    }

    float4 c = _InteractionRT.SampleLevel(sampler_InteractionRT, uv, 0);
    float press = saturate(c.b * c.a);

    if (press != press)
        press = 0.0;

    _InstancePress01[index] = press;
}

uint _Count;

[numthreads(256, 1, 1)]
void Clear(uint3 id : SV_DispatchThreadID)
{
    uint index = id.x;
    if (index >= _Count)
        return;
    _InstancePress01[index] = 0.0;
}